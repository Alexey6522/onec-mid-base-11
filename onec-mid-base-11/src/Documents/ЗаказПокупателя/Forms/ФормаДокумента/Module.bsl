
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// {{ Спицын А.Ю. Доработка типовой формы
	НовыйЭлементКонтактноеЛицо = Элементы.Добавить("vo_КонтактноеЛицо", Тип("ПолеФормы"), Элементы.ГруппаШапкаПраво);
	НовыйЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлементКонтактноеЛицо.ПутьКДанным = "Объект.vo_КонтактноеЛицо";
	
	НовыйЭлементГруппаСкидка = Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Элементы.ГруппаШапкаЛево);
	НовыйЭлементГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлементГруппаСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	
	НовыйЭлементСогласованнаяСкидка = Элементы.Добавить("vo_СогласованнаяСкидка", Тип("ПолеФормы"), Элементы.ГруппаСкидка);
	НовыйЭлементСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлементСогласованнаяСкидка.ПутьКДанным = "Объект.vo_СогласованнаяСкидка";
	//@skip-check wrong-string-literal-content
	НовыйЭлементСогласованнаяСкидка.УстановитьДействие("ПриИзменении", "vo_СогласованнаяСкидкаПриИзменении");
	
	НоваяКомандаПересчетДокумента = Команды.Добавить("vo_ПересчетДокумента");
	НоваяКомандаПересчетДокумента.Действие = "vo_ПересчетДокумента";
	НоваяКомандаПересчетДокумента.Заголовок = "Пересчет документа";

	НовыйЭлементПересчетДокумента = Элементы.Добавить("vo_ПересчетДокумента", Тип("КнопкаФормы"), Элементы.ГруппаСкидка);
	НовыйЭлементПересчетДокумента.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
 	НовыйЭлементПересчетДокумента.ИмяКоманды = "vo_ПересчетДокумента";
	НовыйЭлементПересчетДокумента.Отображение = ОтображениеКнопки.КартинкаИТекст;
	НовыйЭлементПересчетДокумента.Картинка = БиблиотекаКартинок.Перечитать;
	// }}
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

//@skip-check module-structure-form-event-regions
&НаКлиенте
Процедура vo_СогласованнаяСкидкаПриИзменении()
	
	// {{ Спицын АЮ Обработка при изменении поля vo_СогласованнаяСкидка
	Оповещение = Новый ОписаниеОповещения("vo_ПослеЗакрытияВопроса", ЭтотОбъект);
	ЗаголовокВопроса = "Вопрос";
	ТекстВопроса = "Пересчитать сумму документа?";
	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить(КодВозвратаДиалога.Да, "Да");
	СписокКнопок.Добавить(КодВозвратаДиалога.Нет, "Нет");
	ПоказатьВопрос(Оповещение, ТекстВопроса, СписокКнопок,,,ЗаголовокВопроса);
	// }}
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

//@skip-check module-structure-form-event-regions, module-unused-method 
&НаКлиенте
Процедура vo_ПересчетДокумента(Команда)
	// {{ Спицын АЮ
	vo_ВызовРассчитатьСуммуСтроки();
	// }}
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	// {{ Спицын А.Ю. - Закоментировал штатный код	
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	// }}

	// {{ Спицын А.Ю. - Добавил расчет суммы документа с учетом скидки
	СуммаБезСкидки = (ТекущиеДанные.Цена * ТекущиеДанные.Количество);
	ТекущиеДанные.Сумма = СуммаБезСкидки - (СуммаБезСкидки / 100 * Объект.vo_СогласованнаяСкидка);
	// }}
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура vo_ВызовРассчитатьСуммуСтроки()
	
	// {{ Спицын АЮ Добавил расчет суммы документа с учетом скидки
	Для Каждого СтрокаТовар Из Объект.Товары Цикл
		РассчитатьСуммуСтроки(СтрокаТовар);
	КонецЦикла;
	Для Каждого СтрокаУслуга Из Объект.Услуги Цикл
		РассчитатьСуммуСтроки(СтрокаУслуга);
	КонецЦикла;
	// }}
	
КонецПроцедуры

&НаКлиенте
Процедура vo_ПослеЗакрытияВопроса(Результат, ПараметрыОповещения) Экспорт

	// {{ Спицын АЮ
	Если Результат = КодВозвратаДиалога.Нет Или Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		vo_ВызовРассчитатьСуммуСтроки();
	КонецЕсли;
    // }}
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
